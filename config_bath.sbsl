метод Скрипт()
    знч ВерсииПлатформы = ["8.3.18.1902", "8.3.19.1665", "8.3.20.2076", "8.3.21.1607", "8.3.22.1681"]

    знч ИБ = "F:\\BASE1C\\DemoHRM31"
    знч CF_FILES = "F:\\TMP_F\\CF_FILES"
    знч ПолныйПутьКЛогФайлу = "F:\\TMP_F\\conf.log"

    пер ЛогФайл = новый Файл(ПолныйПутьКЛогФайлу)
    если ЛогФайл.Существует()
        Файлы.Удалить(ПолныйПутьКЛогФайлу)
    ;

    для ВерсияПлатформы из ВерсииПлатформы
        для Индекс = 0 по 4
            bath(ВерсияПлатформы, ИБ, CF_FILES, ПолныйПутьКЛогФайлу)
        ;
    ;

    для ВерсияПлатформы из ВерсииПлатформы
        для Индекс = 0 по 4
            agent(ВерсияПлатформы, ИБ, CF_FILES, ПолныйПутьКЛогФайлу)
        ;
    ;

    для ВерсияПлатформы из ВерсииПлатформы
        для Индекс = 0 по 4
            ibcmd(ВерсияПлатформы, ИБ, CF_FILES, ПолныйПутьКЛогФайлу)
        ;
    ;
;

метод bath(ВерсияПлатформы: Строка, ИБ: Строка, CF_FILES: Строка, ПолныйПутьКЛогФайлу: Строка)
    ОчиститьКаталог(CF_FILES)

    знч ProgramFiles = СредаИсполнения.ПолучитьПеременную("ProgramFiles")
    пер КомандаЗапуска = "%ProgramFiles\\1cv8\\%ВерсияПлатформы\\bin\\1cv8.exe"
    пер Аргументы = ["DESIGNER", "/F %ИБ", "/N admin", "/P 123", "/DumpConfigToFiles %CF_FILES"]
    пер Конфигуратор = новый ПроцессОс(КомандаЗапуска, Аргументы, Ложь)

    пер ВремяСтарта: Время = Время.Сейчас()
    Конфигуратор.Запустить()
    Конфигуратор.ОжидатьЗавершения()
    пер ВремяЗавершения: Время = Время.Сейчас()

    пер Длительность = ВремяЗавершения - ВремяСтарта
    пер РезультатВыгрузки =
        "BATH\т%ВерсияПлатформы\т$ВремяСтарта\т$ВремяЗавершения\т%Длительность\т%{Длительность.ВМиллисекундах()}"
    Консоль.Записать(РезультатВыгрузки)
    ЗаписатьВЛог(ПолныйПутьКЛогФайлу, РезультатВыгрузки)
;

метод agent(ВерсияПлатформы: Строка, ИБ: Строка, CF_FILES: Строка, ПолныйПутьКЛогФайлу: Строка)
    ОчиститьКаталог(CF_FILES)

    // Адрес и порт агента по умолчанию /AgentListenAddress 127.0.0.1 /AgentPort 1543
    знч ProgramFiles = СредаИсполнения.ПолучитьПеременную("ProgramFiles")
    пер КомандаЗапуска = "%ProgramFiles\\1cv8\\%ВерсияПлатформы\\bin\\1cv8.exe"
    пер Аргументы =
        ["DESIGNER", "/F %ИБ", "/AgentMode", "/AgentSSHHostKeyAuto", "/AgentBaseDir F:\\TMP_F\\AGENT", "/Visible"]
    пер АгентКонфигуратора = новый ПроцессОс(КомандаЗапуска, Аргументы)
    АгентКонфигуратора.Запустить()
    АгентКонфигуратора.ОжидатьЗавершения(5с)

    // логин / пароль соединения - это логин / пароль пользователя ИБ, для SSH логин должен быть на латининце
    исп СоединениеSsh = новый СоединениеSsh("127.0.0.1", 1543, "admin", "123")
    исп КонсольSsh = СоединениеSsh.ОткрытьКонсоль()
    КонсольSsh.СтрокаПриглашения = "designer> "

    КонсольSsh.ОжидатьПриглашение(5с)
    КонсольSsh.Выполнить("common connect-ib", 2с)
    пер ВремяСтарта: Время = Время.Сейчас()
    КонсольSsh.Выполнить("config dump-config-to-files --dir %CF_FILES", 60с)
    пер ВремяЗавершения: Время = Время.Сейчас()
    КонсольSsh.Выполнить("common disconnect-ib", 2с)
    КонсольSsh.Выполнить("common shutdown", 2с)

    КонсольSsh.Закрыть()
    СоединениеSsh.Закрыть()
    АгентКонфигуратора.Остановить()
    АгентКонфигуратора.ОжидатьЗавершения(2с)

    пер Длительность = ВремяЗавершения - ВремяСтарта
    пер РезультатВыгрузки =
        "AGENT\т%ВерсияПлатформы\т$ВремяСтарта\т$ВремяЗавершения\т%Длительность\т%{Длительность.ВМиллисекундах()}"
    Консоль.Записать(РезультатВыгрузки)
    ЗаписатьВЛог(ПолныйПутьКЛогФайлу, РезультатВыгрузки)
;

метод ibcmd(ВерсияПлатформы: Строка, ИБ: Строка, CF_FILES: Строка, ПолныйПутьКЛогФайлу: Строка)
    ОчиститьКаталог(CF_FILES)

    знч ProgramFiles = СредаИсполнения.ПолучитьПеременную("ProgramFiles")
    пер КомандаЗапуска = "%ProgramFiles\\1cv8\\%ВерсияПлатформы\\bin\\ibcmd.exe"
    пер Аргументы = ["infobase", "config", "export", "%CF_FILES", "--db-path=%ИБ", "--user=admin", "--password=123"]
    пер АвтономныйСервер = новый ПроцессОс(КомандаЗапуска, Аргументы, Ложь)

    пер ВремяСтарта: Время = Время.Сейчас()
    АвтономныйСервер.Запустить()
    АвтономныйСервер.ОжидатьЗавершения()
    пер ВремяЗавершения: Время = Время.Сейчас()

    пер Длительность = ВремяЗавершения - ВремяСтарта
    пер РезультатВыгрузки =
        "IBCMD\т%ВерсияПлатформы\т$ВремяСтарта\т$ВремяЗавершения\т%Длительность\т%{Длительность.ВМиллисекундах()}"
    Консоль.Записать(РезультатВыгрузки)
    ЗаписатьВЛог(ПолныйПутьКЛогФайлу, РезультатВыгрузки)
;

метод ОчиститьКаталог(CF_FILES: Строка)
    пер Каталог = новый Файл(CF_FILES)

    если Каталог.Существует() и Каталог.ЯвляетсяКаталогом() и не Файлы.КаталогПустой(CF_FILES)!
        Файлы.Удалить(CF_FILES)
    ;
;

метод ЗаписатьВЛог(ПолныйПутьКЛогФайлу: Строка, ДанныеЛога: Строка)
    пер Лог = новый Файл(ПолныйПутьКЛогФайлу)
    если не Лог.Существует()
        Файлы.Создать(ПолныйПутьКЛогФайлу)
    ;

    исп Поток = Лог.ОткрытьПотокЗаписи(ЗаписатьВКонец = Истина)
    если не Лог.Размер == 0
        Поток.Записать(Символы.НОВАЯ_СТРОКА)
    ;

    Поток.Записать(ДанныеЛога)
    Поток.Закрыть()
;